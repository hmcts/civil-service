import uk.gov.hmcts.rse.AuthMode

plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.11'
  id 'org.owasp.dependencycheck' version '12.1.3'
  id 'com.github.ben-manes.versions' version '0.52.0'
  id 'org.sonarqube' version '6.2.0.5505'
  id 'au.com.dius.pact' version '4.6.17'
  id "io.freefair.lombok" version "8.14.4"
  id 'com.github.hmcts.rse-cft-lib' version '0.19.2017'
  id "org.flywaydb.flyway" version "10.22.0"
  id 'maven-publish'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

def versions = [
  junit             : '5.14.3',
  junitPlatform     : '1.14.3',
  springBoot        : springBoot.class.package.implementationVersion,
  springStatemachine: '4.0.1',
  lombok            : '1.18.38',
  camunda           : '7.24.1',
  testcontainers    : '1.21.3',
  pdfbox            : '3.0.6',
  tika              : '3.2.3'
]

allprojects {
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'checkstyle'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'org.owasp.dependencycheck'
  apply plugin: 'com.github.ben-manes.versions'

  checkstyle {
    maxWarnings = 0
    toolVersion = '10.26.1'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
  }

  jacoco {
    toolVersion = '0.8.12' // jacocoMavenPluginVersion
    reportsDirectory = file("$buildDir/reports/jacoco")
  }

// before committing a change, make sure task still works
  dependencyUpdates {
    def isNonStable = { String version ->
      def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
      def regex = /^[0-9,.v-]+$/
      return !stableKeyword && !(version ==~ regex)
    }
    rejectVersionIf { selection -> // <---- notice how the closure argument is named
      return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
    }
  }

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
  dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
      // Disable scanning of .NET related binaries
      assemblyEnabled = false
      retirejs {
        enabled = false
      }
      ossIndex {
        enabled = false
      }
    }
  }

  dependencyManagement {
    dependencies {
      //CVE-2018-10237 - Unbounded memory allocation
      dependencySet(group: 'com.google.guava', version: '33.5.0-jre') {
        entry 'guava'
      }

      dependency group: 'com.squareup.okhttp3', name: 'okhttp', version: '5.3.2'

      // To prevent other libraries using conflicting versions
      dependency group: 'org.slf4j', name: 'slf4j-nop', version: '2.0.17'

      dependencySet(group: 'org.apache.httpcomponents.core5', version: '5.4.1') {
        entry 'httpcore5'
        entry 'httpcore5-h2'
      }

    }
    imports {
      mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2023.0.6'
      mavenBom "com.azure.spring:spring-cloud-azure-dependencies:5.24.1"
    }
  }

  configurations {
    testCompile.exclude group: 'ch.qos.logback', module: 'logback-classic'
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url "https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1"
    }
  }
}

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:-removal" << "-Werror"
}

test {
  failFast = true
}

tasks.withType(Copy) {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task integration(type: Test) {
  maxHeapSize = '3G'
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
  testLogging.showStandardStreams = false

}

task fortifyScan(type: JavaExec, description: 'Run fortify scan.') {
  getMainClass().set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

task awaitApplicationReadiness(type: Exec, description: 'Awaits until application is ready.') {
  commandLine './bin/wait-for.sh'
  commandLine './bin/wait-for.sh', 'manage-case'
}

task runSmokeTests(type: Exec, description: 'Runs smoke tests.') {
  commandLine '/usr/bin/yarn', '--silent', 'run', 'test:smoke'
}

task runApiTests(type: Exec, description: 'Runs Prod features in functional tests.') {
  onlyIf {
    System.env.NIGHTLY_RUN != 'true'
  }
  commandLine './bin/run-api-tests.sh'
}

task runNightlyApiTests(type: Exec, description: 'Runs nightly tests.') {
  onlyIf {
    return System.env.NIGHTLY_RUN == 'true'
  }
  commandLine './bin/run-nightly-api-tests.sh'
}

def inStrictOrder(Task... tasks) {
  for (int i = 0; i < tasks.size() - 1; i++) {
    tasks[i + 1].mustRunAfter(tasks[i])
  }
  return tasks
}

task smoke(description: 'Runs the smoke tests.') {
  dependsOn(inStrictOrder(awaitApplicationReadiness, runSmokeTests))
}

task functional(description: 'Runs the functional tests.') {
  dependsOn(inStrictOrder(awaitApplicationReadiness,
    runNightlyApiTests, runApiTests))
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

// excluding controllers from Sonar due to https://tools.hmcts.net/jira/browse/DTSCCI-254
// https://tools.hmcts.net/jira/browse/DTSCCI-311 ticket to write unit test for each controller

sonarqube {
  properties {
    property "sonar.projectName", "CIVIL :: service"
    property "sonar.projectKey", "civil-service"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
    property "sonar.coverage.exclusions", "**/model/**, **/config/**/*Configuration.java, **/request/servlet/** , **/controllers/**, **/testingsupport/**, **/*ExternalTaskListener.java, **/*BaseExternalTaskHandler.java, **/stereotypes/**, **/*Exception.java, **/EventHistoryMapper*.java, **/model/hearingvalues/**, **/enums/hearing/**, **/fees/client/**, **/enums/sdo/**, **/service/PaymentsService.java, **/DashboardWithParamsCallbackHandler.java, **/PaymentRequestUpdateCallbackService.java, **/advice/**, **/dashboard/cache/**"
    property "sonar.cpd.exclusions", "**/*DocumentManagementService.java, **/*Spec*.java, **/*CcdDashboardClaimantClaimMatcher.java, **/*CcdDashboardDefendantClaimMatcher.java"
    property "sonar.exclusions", "**/model/**, **/hmc/model/**, **/model/hearingvalues/**, **/handler/callback/camunda/dashboardnotifications/claimant/CCJRequestedDashboardNotificationHandler.java, **/handler/callback/camunda/dashboardnotifications/claimant/ClaimantCCJResponseNotificationHandler.java" +
      ", **/handler/callback/camunda/dashboardnotifications/claimant/ClaimantResponseNotificationHandler.java, **/utils/**, **/filters/**, **/handler/callback/camunda/dashboardnotifications/defendant/MoreTimeRequestedDashboardNotificationDefendantHandler.java, **/handler/callback/camunda/dashboardnotifications/claimant/ClaimantMediationSuccessfulDashboardNotificationHandler.java, **/handler/callback/camunda/dashboardnotifications/claimant/ClaimSettledDashboardNotificationHandler.java, **/handler/callback/camunda/dashboardnotifications/claimant/HearingScheduledClaimantNotificationHandler.java, **/handler/callback/camunda/dashboardnotifications/defendant/SettleClaimPaidInFullDefendantDashboardNotificationHandler.java, **/handler/callback/camunda/dashboardnotifications/defendant/DefendantNotifyDiscontinuanceDashboardNotificationHandler.java" +
      ",**/utils/CaseQueriesUtil.java, **/handler/callback/user/RaiseQueryCallbackHandler.java,**/handler/migration/MigrateCaseQueriesTask.java, **/handler/callback/user/RespondQueryCallbackHandler.java, **/service/servicebus/CcdCaseEventsExecutor.java, **/tasklisteners/MigrateCasesTaskListener.java, **/migration/MigrationTask.java, **/migration/CustomRequestScopeAttr.java"
    property "sonar.host.url", "https://sonar.reform.hmcts.net/"
    property "sonar.web.javaOpts", "-Xmx2G"
  }
}

configure(subprojects.findAll { it.name == 'dashboard-notifications' }) {
  sonarqube {
    properties {
      property "sonar.coverage.jacoco.xmlReportPaths", "${rootProject.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
  }
}

jacocoTestReport {
  dependsOn "bootStartScripts"
  executionData(test, integration)
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
  subprojects.each {
    sourceSets it.sourceSets.main
  }
  reports {
    xml.required.set(true)
    csv.required.set(false)
    xml.outputLocation.set(file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml"))
  }
  dependsOn tasks.named('bootStartScripts')
  dependsOn tasks.named('startScripts')
}

jacocoTestReport.dependsOn {
  subprojects*.test
}

jacocoTestReport.dependsOn subprojects.collect { project ->
  project.tasks.findByName('jacocoTestReport')
}.findAll { it != null }

jacocoTestReport.dependsOn tasks.matching {
  it.name.startsWith('compile')
          || it.name.startsWith('process')
          || it.name.equals('startScripts')
          || it.name.equals('bootStartScripts')
}

tasks.named('test') {
  finalizedBy jacocoTestReport
}

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

allprojects {
  configurations.all {
    resolutionStrategy.eachDependency { details ->
      if (details.requested.group == 'org.projectlombok') {
        details.useVersion versions.lombok
      }
      if (details.requested.group == 'io.netty') {
        if (details.requested.name in [
          'netty-buffer', 'netty-codec', 'netty-codec-dns',
          'netty-codec-http', 'netty-codec-http2', 'netty-codec-socks',
          'netty-common', 'netty-handler', 'netty-handler-proxy',
          'netty-resolver', 'netty-resolver-dns', 'netty-resolver-dns-classes-macos',
          'netty-resolver-dns-native-macos',
          'netty-transport', 'netty-transport-classes-epoll',
          'netty-transport-classes-kqueue', 'netty-transport-native-epoll',
          'netty-transport-native-kqueue', 'netty-transport-native-unix-common'
        ]) {
          details.useVersion('4.1.129.Final')
          details.because('CVE-2025-25193, CVE-2025-67735')
        }
      }
      if (details.requested.group == 'org.apache.logging.log4j') {
        details.useVersion('2.25.3')
        details.because('CVE-2025-68161 vendor recommended version')
      }
    }
    exclude group: 'org.springframework.security', module: 'spring-security-rsa'
    exclude group: 'org.projectlombok', module: 'lombok-mapstruct-binding'
  }
}

dependencies {
  def withoutJunit4 = {
    exclude group: 'junit', module: 'junit'
  }
  implementation project(':dashboard-notifications')
  implementation group: 'org.springframework.data', name: 'spring-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework', name: 'spring-aspects'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'

  implementation group: 'org.springframework.statemachine', name: 'spring-statemachine-core', version: versions.springStatemachine
  implementation group: 'org.springframework.statemachine', name: 'spring-statemachine-test', version: versions.springStatemachine

  // CVE-2025-48734 force update of commons-beanutils.
  implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.11.0'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.16'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.10'
  implementation group: 'jakarta.persistence', name: 'jakarta.persistence-api', version: '3.2.0'

  implementation group: 'org.jdbi', name: 'jdbi3-sqlobject', version: '3.51.0'
  implementation group: 'org.jdbi', name: 'jdbi3-spring5', version: '3.49.5'

  implementation group: 'org.flywaydb', name: 'flyway-core'

  implementation group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '10.22.0'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-openfeign-core'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2'
  implementation group: 'org.apache.tika', name: 'tika-core', version: versions.tika
  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '13.9'
  implementation group: 'org.springframework.retry', name: 'spring-retry'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '8.0.0'
  implementation group: 'com.microsoft.azure', name: 'applicationinsights-web', version: '3.7.7'

  implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.2.0'
  implementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'
  implementation group: 'com.github.hmcts', name: 'send-letter-client', version: '3.0.23.1'
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version:'7.4'
  implementation('com.github.hmcts:fees-java-client:0.1.0') {
    exclude group: 'uk.gov.hmcts.reform', module: 'java-logging'
  }
  implementation('com.github.hmcts:payments-java-client:1.6.7') {
    exclude group: 'uk.gov.hmcts.reform', module: 'java-logging'
    exclude group: 'uk.gov.hmcts.reform', module: 'service-auth-provider-client'
  }
  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.2.1-RELEASE'
  implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.59.2'
  implementation group: 'com.sendgrid', name: 'sendgrid-java', version: '4.10.3'

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.2.2'

  implementation group: 'org.springframework.security', name: 'spring-security-web'
  implementation group: 'org.springframework.security', name: 'spring-security-config'
  implementation group: 'org.springframework.security', name: 'spring-security-core'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-resource-server'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-client'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-core'
  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '10.8'
  implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.12.6'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '2.1.2'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.25.3'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.25.3'
  constraints {
    implementation('org.apache.logging.log4j:log4j-core:2.25.3') {
      because 'CVE-2025-68161 (upgrade to vendor recommended version)'
    }
  }
  implementation group: 'org.apache.commons', name: 'commons-text', version: '1.15.0'
  implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.29'
  implementation group: 'com.networknt', name: 'json-schema-validator', version: '1.5.8'

  implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin'
  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
  implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv'

  implementation group: 'org.camunda.bpm', name: 'camunda-external-task-client', version: versions.camunda
  implementation group: 'org.camunda.community.rest', name: 'camunda-platform-7-rest-client-spring-boot-starter', version: versions.camunda
  implementation group: 'org.camunda.bpm', name: 'camunda-engine-rest-core', version: versions.camunda

  implementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'

  // JAX-B dependencies for JDK 9+
  implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '4.0.5'
  implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '4.0.6'
  implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
  runtimeOnly group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.9'
  runtimeOnly group: 'com.sun.activation', name: 'javax.activation', version: '1.2.0'

  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '6.3.0'

  implementation group: 'com.azure', name: 'azure-core', version: '1.57.1'
  implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.17.17'

  implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: versions.pdfbox
  implementation group: 'org.apache.pdfbox', name: 'pdfbox-io', version: versions.pdfbox

  implementation group: 'com.github.hmcts', name: 'auth-checker-lib', version: '3.1.1'

  implementation 'org.apache.poi:poi:5.5.1'
  implementation 'org.apache.poi:poi-ooxml:5.5.1'
  implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.6'

  testImplementation group: 'org.apache.pdfbox', name: 'pdfbox-io', version: versions.pdfbox
  testImplementation group: 'org.apache.pdfbox', name: 'preflight', version: versions.pdfbox, withoutJunit4
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.11.0'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.11.0'
  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.11.0'
  testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.17.6'
  testImplementation group: 'net.bytebuddy', name: 'byte-buddy-agent', version: '1.17.6'

  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation libraries.junit5
  testRuntimeOnly "org.junit.platform:junit-platform-launcher:${versions.junitPlatform}"
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  testImplementation group: 'io.rest-assured', name: 'rest-assured'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.7'

  testImplementation group: 'org.springframework.statemachine', name: 'spring-statemachine-test', version: versions.springStatemachine
  testImplementation 'com.github.hmcts:fortify-client:1.4.9:all'
  //pact contract testing
  contractTestImplementation 'au.com.dius.pact.consumer:junit5:4.6.17'
  contractTestImplementation 'au.com.dius.pact.provider:junit5:4.6.17'
  contractTestImplementation 'au.com.dius.pact.provider:junit5spring:4.6.17'
  contractTestImplementation group: 'org.hamcrest', name: 'java-hamcrest', version: '2.0.0.0'
  contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.14.3')
  contractTestImplementation('org.junit.jupiter:junit-jupiter-engine:5.14.3')

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath

  integrationTestImplementation('org.junit.jupiter:junit-jupiter-api:5.14.3')
  integrationTestImplementation('org.junit.jupiter:junit-jupiter-engine:5.14.3')

  testImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testcontainers
  testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testcontainers
  testImplementation group: 'com.h2database', name: 'h2', version: '2.4.240'
  // dashboard specific changes ends


  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  // https://mvnrepository.com/artifact/com.opencsv/opencsv
  implementation group: 'com.opencsv', name: 'opencsv', version: '5.12.0', {
    exclude group: 'commons-collections', module: 'commons-collections'
  }
}

mainClassName = 'uk.gov.hmcts.reform.civil.Application'

bootJar {
  getArchiveFileName().set(provider {
    'civil-service.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

task buildCCDXlsx(type: Exec) {
  group 'ccd tasks'

  environment 'CCD_DEF_CASE_SERVICE_BASE_URL', 'http://localhost:4000'
  environment 'CCD_DEF_GEN_APP_SERVICE_BASE_URL', 'http://localhost:9999'
  environment 'CCD_DEF_AAC_URL', 'http://localhost:4502'
  commandLine './bin/build-civil-xlsx-definitions.sh'
}

task runOtherServices(type: Exec) {
  group 'ccd tasks'

  environment 'ADDITIONAL_COMPOSE_FILES', 'cftlib-additional-services.yml'
  environment 'BASE_COMPOSE_FILES_PATH', './src/cftlib/resources/docker/'

  commandLine './bin/add-services.sh'
  doLast() {
    importBpmnDiagrams
  }
}

task importBpmnDiagrams(type: Exec) {
  group 'ccd tasks'

  dependsOn(runOtherServices)

  environment 'CAMUNDA_BASE_URL', 'http://localhost:9404'
  environment 'SERVICE_AUTH_PROVIDER_API_BASE_URL', 'http://rpe-service-auth-provider-aat.service.core-compute-aat.internal'

  commandLine './bin/import-bpmn-diagram-cftlib.sh'
}

bootWithCCD {
  group 'ccd tasks'

  dependsOn(buildCCDXlsx)
  dependsOn(runOtherServices)
  dependsOn(importBpmnDiagrams)

  //AAT or Local (IDAM and S2S Simulators)
  authMode = AuthMode.AAT

  environment 'BASE_COMPOSE_FILES_PATH', './src/cftlib/resources/docker/'
  environment 'RSE_LIB_ADDITIONAL_DATABASES', 'cmc'

  doFirst() {
    project.file('./.aat-env').readLines().each() {
      def index = it.indexOf("=")
      def key = it.substring(0, index)
      def value = it.substring(index + 1)
      environment(key, value)
    }
  }

}


project.ext {
  pactVersion = getCheckedOutGitCommitHash()
}

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'pacts'
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
    consumerBranch = System.getenv("PACT_BRANCH_NAME") ?: 'Dev'
    version = project.pactVersion
  }
}

tasks.register('contract', Test) {
  group = 'Delivery pipeline'
  description = 'Runs the consumer Pact tests'
  setTestClassesDirs(sourceSets.contractTest.output.classesDirs)
  setClasspath(sourceSets.contractTest.runtimeClasspath)
  useJUnitPlatform()
  systemProperty 'pact.rootDir', "pacts"
}

tasks.register('runAndPublishConsumerPactTests', Test) {
  dependsOn(contract)
  logger.lifecycle("Runs pact Tests")
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  finalizedBy(pactPublish)
}

tasks.register('providerContractTests', Test) {
  description = 'Runs ONLY the provider Pact verification tests.'
  group = 'Verification'

  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  include '**/provider/**'
}

tasks.register('runProviderPactVerification') {
  description = 'Entry point for Jenkins to run provider contract tests.'
  group = 'Verification'

  dependsOn tasks.named('providerContractTests')
}

tasks.withType(Test).configureEach { t ->
  useJUnitPlatform()

  maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
  jvmArgs '-Xshare:off'

  // Keep within typical Jenkins container limits
  minHeapSize = "256m"
  maxHeapSize = "1536m"
  jvmArgs "-XX:MaxMetaspaceSize=512m"
  jvmArgs '-XX:ReservedCodeCacheSize=512m'
  // Temurin 21.0.7 occasionally crashes in C2 (BoolNode::Ideal SIGSEGV); stay on C1 until upstream fix.
  jvmArgs "-XX:TieredStopAtLevel=1"

  testLogging {
    exceptionFormat = 'full'
    showCauses = true
    showExceptions = true
    showStackTraces = true
    showStandardStreams = false
    events "failed", "skipped"
  }

  if (project.hasProperty('pact.verifier.publishResults')) {
    systemProperty 'pact.verifier.publishResults', project.property('pact.verifier.publishResults')
  }
  if (project.hasProperty('pact.provider.version')) {
    systemProperty 'pact.provider.version', project.property('pact.provider.version')
  } else {
    systemProperty 'pact.provider.version', project.pactVersion
  }

  if (project.hasProperty('pact.provider.branch')) {
    systemProperty 'pact.provider.branch', project.property('pact.provider.branch')
  } else {
    systemProperty 'pact.provider.branch', System.getenv("PACT_BRANCH_NAME") ?: "Dev"
  }
}

def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

task loadEnvSecrets() {
  doLast {
    new ByteArrayOutputStream().withStream { os ->
      exec {
        commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'civil-aat', '-o', 'tsv', '--query', 'value', '--name', 'civil-service-dot-env'
        standardOutput = os
      }
      project.file('./.aat-env').write(new String(os.toString().replace('\n', '').decodeBase64(), java.nio.charset.StandardCharsets.UTF_8))
    }
  }
}

bootRun {
  doFirst() {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').readLines().each() {
        def index = it.indexOf("=")
        def key = it.substring(0, index)
        def value = it.substring(index + 1)
        environment key, value
      }
    }
  }
}

publishing {
  repositories {
    maven {
      name = "AzureArtifacts"
      url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
      credentials {
        username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
        password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
      }
    }
  }
}
